plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

evaluationDependsOnChildren()

version = "${majorVersion}.${minorVersion}.${patchVersion}"
if (!"${postfixVersion}".isEmpty()) {
    version += "-${postfixVersion}"
}

group = "net.roguelogix.${mod_id}"
base {
    archivesName = "${mod_id}-${mc_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file("run/${it.name}")

            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
                Quartz {
                    source project(':Quartz').sourceSets.main
                }
                Phosphophyllite {
                    source project(':Phosphophyllite').sourceSets.main
                }
            }
        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    config 'quartz.mixins.json'
    config 'phosphophyllite.mixins.json'
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // mekanism
        url 'https://modmaven.dev/'
    }
    maven {
        // computer craft
        url 'https://squiddev.cc/maven/'
        content {
            includeGroup("cc.tweaked")
            includeModule("org.squiddev", "Cobalt")
        }
    }
}

dependencies {
    minecraft "net.neoforged:forge:${mc_version}-${neo_version}"

    compileOnly project(':Phosphophyllite')
    compileOnly project(':Quartz')

    compileOnly("cc.tweaked:cc-tweaked-${cc_mc_version}-core-api:${cc_version}")
    compileOnly(fg.deobf("cc.tweaked:cc-tweaked-${cc_mc_version}-forge-api:${cc_version}"))
    runtimeOnly(fg.deobf("cc.tweaked:cc-tweaked-${cc_mc_version}-forge:${cc_version}"))

    compileOnly(fg.deobf("mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${jei_mc_version}-forge-api:${jei_version}"))
    // at runtime, use the full JEI jar for Forge
    runtimeOnly(fg.deobf("mezz.jei:jei-${jei_mc_version}-forge:${jei_version}"))

    compileOnly "mekanism:Mekanism:1.19.2-10.3.5.474:api"

//    runtimeOnly fg.deobf('curse.maven:MEK-268560:4041164')
//    runtimeOnly fg.deobf('curse.maven:MEKG-268566:4041168')
//    runtimeOnly fg.deobf('curse.maven:COFHCore-69162:4385216')
//    runtimeOnly fg.deobf('curse.maven:ThermalFoundation-222880:4382373')
//    runtimeOnly fg.deobf('curse.maven:ThermalExpansion-69163:4382371')
//    runtimeOnly fg.deobf('curse.maven:ThermalDynamics-227443:4391160')

//    runtimeOnly fg.deobf('curse.maven:Rubidium-574856:4448157')
    runtimeOnly fg.deobf('curse.maven:Oculus-581495:4578741')

//    runtimeOnly fg.deobf('curse.maven:WorldStripper-250603:3545856')
//    runtimeOnly fg.deobf('curse.maven:CW-399558:3926815')

    minecraftLibrary("org.lwjgl:lwjgl-opencl:3.3.1") {
        transitive(false)
    }
    minecraftLibrary ("org.lwjgl:lwjgl-vulkan:3.3.1"){
        transitive(false)
    }
}

gradle.projectsEvaluated {
    var phosVersionRange = project.project(":Phosphophyllite").property("compatibility").toString().split(',')
    var phosMinVersion = phosVersionRange[0].substring(1)
    var phosMaxVersion = phosVersionRange[1].substring(1, phosVersionRange[1].length() - 1)
    var phosRequirementString = "Requires [Phosphophyllite](https://www.curseforge.com/minecraft/mc-mods/phosphophyllite) version of at least " + phosMinVersion + " but less than " + phosMaxVersion

    var quartzVersionRange = project.project(":Quartz").property("compatibility").toString().split(',')
    var quartzMinVersion = quartzVersionRange[0].substring(1)
    var quartzMaxVersion = quartzVersionRange[1].substring(1, quartzVersionRange[1].length() - 1)
    var quartzRequirementString = "Requires [Quartz](https://www.curseforge.com/minecraft/mc-mods/quartz) version of at least " + quartzMinVersion + " but less than " + quartzMaxVersion

    println(phosRequirementString)
    println(quartzRequirementString)

    println("::set-output name=phos_version_range::" + phosRequirementString)
    println("::set-output name=quartz_version_range::" + quartzRequirementString)

    tasks.withType(ProcessResources).configureEach {
        var replaceProperties = [
                minecraft_version   : mc_version,
                neo_version         : neo_version,
                loader_version      : neo_version.substring(0, neo_version.indexOf('.')),
                version             : version,
                phos_version_range  : project.project(":Phosphophyllite").property("compatibility").toString(),
                quartz_version_range: project.project(":Quartz").property("compatibility").toString(),
        ]
        inputs.properties replaceProperties

        filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
            expand replaceProperties + [project: project]
        }
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : "BiggerSeries",
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : "BiggerSeries",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.named('publish').configure {
    dependsOn 'reobfJar'
    mustRunAfter 'reobfJar'
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/publishlocal"
        }
    }
}

// NeoGradle is dumb, and generates the run configs incorrectly, this fixes them
tasks.register('zzz_copyRunConfigsToTmpDir', Copy) {
    from '.idea/runConfigurations'
    into '.idea/runConfigurations1'
}

tasks.register('zzz_depeteTmpDir', Delete) {
    delete '.idea/runConfigurations1'
}

tasks.register('_createIntellijRunsThatWork', Copy) {
    dependsOn 'genIntellijRuns'
    mustRunAfter 'genIntellijRuns'
    dependsOn 'zzz_copyRunConfigsToTmpDir'
    mustRunAfter 'zzz_copyRunConfigsToTmpDir'

    from '.idea/runConfigurations1'
    into '.idea/runConfigurations'
    filter { String line -> line.replace("out/production/classes", "build/classes/java/main") }
    filter { String line -> line.replace("out/production/resources", "build/resources/main") }
    filter { String line -> line.replace("copyIntellijResources", "prepareRunClientCompile") }

    finalizedBy 'zzz_depeteTmpDir'
}